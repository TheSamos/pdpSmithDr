set(CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS true)


macro(SMITHDR_OPTION_INIT)
  set(SMITHDR_OPTIONS)
endmacro()


macro(SMITHDR_OPTION _name _description _default _sys)
  set(_msg OFF)
  if(${_sys} MATCHES "GCC")
    set(_system CMAKE_COMPILER_IS_GNUCXX)
  else()
    set(_system ${_sys})
  endif()
  if(${_system} MATCHES "ALL")
    option(SMITHDR_${_name} ${_description} ${_default})
    set(_msg ON)
  else()
    if(${${_system}})
      option(SMITHDR_${_name} ${_description} ${_default})
      set(_msg ON)
    endif()
  endif()
  list(APPEND SMITHDR_OPTIONS SMITHDR_${_name})
  set(SMITHDR_${_name}_description ${_description})
  set(SMITHDR_${_name}_show_message ${_msg})
endmacro()


macro(SMITHDR_OPTION_LIST_ALL)
  foreach(_option ${SMITHDR_OPTIONS})
    if(${_option}_show_message)
      string(SUBSTRING "${_option}                            " 0 25 _var)
      if(${_option})
	set(_isset ON)
      else()
	set(_isset OFF)
      endif()
      string(SUBSTRING "${_isset}     " 0 4 _val)
      message(STATUS "${_var}= ${_val}   : ${${_option}_description}")
    endif()
  endforeach()
endmacro()


macro(SMITHDR_GET_MOC_HEADERS _moc_headers _headers)
  set(moc_headers "")
  foreach(header IN ITEMS ${_headers})
    file(STRINGS ${header} found REGEX "Q_OBJECT")
    string(COMPARE NOTEQUAL "${found}" "" not_empty)
    if (${not_empty})
      set(moc_headers ${moc_headers} ${header})
    endif()
  endforeach(header)
  set(${_moc_headers} ${moc_headers})
endmacro()

macro(SMITHDR_REMOVE_DIR_CONTENT dir _result _source)
  set(res "")
  foreach(source IN ITEMS ${_source})
    if (NOT ${source} MATCHES ${dir})
      set(res ${res} ${source})
    endif()
  endforeach(source)
  set(${_result} ${res})
endmacro()
